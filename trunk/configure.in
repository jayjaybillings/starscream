#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ([2.63])
AC_INIT([Starscream], [1.1], [jayjaybillings@gmail.com])
AM_INIT_AUTOMAKE
AC_CONFIG_FILES([Makefile
                 src/Makefile])
AC_CONFIG_SRCDIR([src/])
AC_CONFIG_HEADERS([config.h])

# ----- Options that can be set by users -----
# FFTW3 Override
AC_ARG_WITH(fftw3,AS_HELP_STRING([--with-fftw3=<directory>],
           [Threaded FFTW3 install directory. Configure will expect \
            to find include and lib subdirectories]),
           [FFTW_DIR=$with_fftw3])
if test ! "x$FFTW_DIR" = x;
then
   AC_MSG_RESULT([FFTW Install directory = $FFTW_DIR])
   AC_SUBST(FFTW_DIR)
fi
# GSL Override
AC_ARG_WITH(gsl,AS_HELP_STRING([--with-gsl=<directory>],
           [GNU GSL install directory. Configure will expect \
            to find include and lib subdirectories]),
           [GSL_DIR=$with_gsl])
if test ! "x$GSL_DIR" = x;
then
   AC_MSG_RESULT([GSL Install directory = $FFTW_DIR])
   AC_SUBST(GSL_DIR)
fi

# Checks for programs.
AC_PROG_CC
AC_PROG_LIBTOOL
AC_OPENMP
PKG_PROG_PKG_CONFIG([])

# Checks for standard header files
AC_CHECK_HEADERS([stdlib.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_TYPE_SIZE_T

# ----- Check for the GSL -----
# Set CFLAGS for proper check for threads. 
CFLAGS="$CFLAGS $OPENMP_CFLAGS"
# Check first to see if users want to do a static link to their own install.
if test ! "x$GSL_DIR" = x;
then
   LDFLAGS="${LDFLAGS} -L${GSL_DIR}/lib"
#FIXME
   AC_CHECK_LIB(gsl,gsl_integration_qag,
               [GSL_LIBS="-L${GSL_DIR}/lib -lgsl -lgslcblas"],
               [`cat configWarnings/MISSING_GSL_WARNING 1>&2`],
               [-lgslcblas])
   GSL_CFLAGS="-I$GSL_DIR/include "
   GSL_LIBDIR="$GSL_DIR/lib"
else
# Link to the system versions using PKG-CONFIG
   if test ! "x$PKG_CONFIG" = x;
   then
      PKG_CHECK_MODULES(GSL, gsl)
# If PKG-CONFIG is not installed
   else
      `cat configWarnings/MISSING_PKG_CONFIG 1>&2`
   fi
fi
# Load the GSL variables
AC_SUBST(GSL_CFLAGS)
AC_SUBST(GSL_LIBS)
AC_SUBST(GSL_LIBDIR)
# Check for the GSL headers
CFLAGS="$CFLAGS $GSL_CFLAGS"
AC_CHECK_HEADERS(gsl_math.h)

# ----- Check for FFTW3 -----
# Set CFLAGS for proper check for threads. 
CFLAGS="$CFLAGS $OPENMP_CFLAGS"
# Check first to see if users want to do a static link to their own install.
if test ! "x$FFTW_DIR" = x;
then
   LDFLAGS="${LDFLAGS} -L${FFTW_DIR}/lib"
   # Check for OpenMP support
   if test ! "x$OPENMP_CFLAGS" = x;
   then
      AC_CHECK_LIB(fftw3_threads,fftw_spawn_loop,
                  [FFTW_LIBS="-lfftw3 -lfftw3_threads -lgomp"],
                  [`cat configWarnings/MISSING_FFTW3_WARNING 1>&2`],
                  [-lfftw3])
      AC_DEFINE([USE_FFTW_THREADS],[1], [Define to 1 to use FFTW_THREADS])
   else
      AC_CHECK_LIB(fftw3,fftw_mkplan_d,
                  [FFTW_LIBS="-lfftw3"],
                  [`cat configWarnings/MISSING_FFTW3_WARNING 1>&2`],
                  [-lfftw3])
   fi
   FFTW_CFLAGS="-I$FFTW_DIR/include "
   FFTW_LIBDIR="$FFTW_DIR/lib"
else
# Link to the system versions using PKG-CONFIG
   if test ! "x$PKG_CONFIG" = x;
   then
      PKG_CHECK_MODULES(FFTW, fftw3)
      if test ! "x$FFTW_LIBS" = x;
      then
         if test ! "x$OPENMP_CFLAGS" = x;
         then
            FFTW_LIBS="${FFTW_LIBS} -lfftw3_threads"
         else
            FFTW_LIBS="${FFTW_LIBS} -lfftw3"
         fi
      fi
# If PKG-CONFIG is not installed
   else
      `cat configWarnings/MISSING_PKG_CONFIG 1>&2`
   fi
fi
# Load the FFTW3 variables
AC_SUBST(FFTW_CFLAGS)
AC_SUBST(FFTW_LIBS)
AC_SUBST(FFTW_LIBDIR)
# Check for the FFTW3 headers
CFLAGS="$CFLAGS $FFTW_CFLAGS"
AC_CHECK_HEADERS(fftw3.h)

# Checks for system library functions.
AC_FUNC_MALLOC
AC_CHECK_LIB(m,sin)
AC_CHECK_FUNCS([pow sqrt])

# Set AM Flags
AM_CFLAGS="$GSL_CFLAGS $FFTW_CFLAGS"
AC_SUBST(AM_CFLAGS)

AC_OUTPUT

# ----- Print diagnostic information for the user -----
echo " "
# Diagnostic info for GSL
if test ! "x$GSL_DIR" = x;
then
   echo "GSL_DIR:                    ${GSL_DIR}"
fi
echo "GSL_CFLAGS:                 ${GSL_CFLAGS}"
if test ! "x$GSL_LIBS" = x;
then
   echo "GSL_LIBS:                   ${GSL_LIBS}"
else
   echo "GSL NOT FOUND! Please install it and try again!"
fi
# Diagnostic info for FFTW3
if test ! "x$FFTW_DIR" = x;
then
   echo "FFTW_DIR:                   ${FFTW_DIR}"
fi
echo "FFTW_CFLAGS:                ${FFTW_CFLAGS}"
if test ! "x$FFTW_LIBS" = x;
then
   echo "FFTW_LIBS:                  ${FFTW_LIBS}"
else
   echo "FFTW3 NOT FOUND! Please install it and try again!"
fi
# Diagnostic info for OpenMP
echo "OpenMP compiler flags:      $OPENMP_CFLAGS"
